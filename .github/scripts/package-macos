#!/usr/bin/env zsh

builtin emulate -L zsh
setopt EXTENDED_GLOB
setopt PUSHD_SILENT
setopt ERR_EXIT
setopt ERR_RETURN
setopt NO_UNSET
setopt PIPE_FAIL
setopt NO_AUTO_PUSHD
setopt NO_PUSHD_IGNORE_DUPS
setopt FUNCTION_ARGZERO

## Enable for script debugging
# setopt WARN_CREATE_GLOBAL
# setopt WARN_NESTED_VAR
# setopt XTRACE

if (( ! ${+CI} )) {
  print -u2 -PR "%F{1}    ✖︎ ${ZSH_ARGZERO:t:r} requires CI environment%f"
  exit 1
}

autoload -Uz is-at-least && if ! is-at-least 5.9; then
  print -u2 -PR "${CI:+::error::}%F{1}${funcstack[1]##*/}:%f Running on Zsh version %B${ZSH_VERSION}%b, but Zsh %B5.2%b is the minimum supported version. Upgrade Zsh to fix this issue."
  exit 1
fi

TRAPZERR() {
  print -u2 -PR "::error::%F{1}    ✖︎ script execution error%f"
  print -PR -e "
  Callstack:
  ${(j:\n     :)funcfiletrace}
  "

  exit 2
}

package() {
  if (( ! ${+SCRIPT_HOME} )) typeset -g SCRIPT_HOME=${ZSH_ARGZERO:A:h}
  local host_os='macos'
  local project_root=${SCRIPT_HOME:A:h:h}
  local buildspec_file=${project_root}/buildspec.json

  fpath=("${SCRIPT_HOME}/utils.zsh" ${fpath})
  autoload -Uz log_group log_error log_output check_macos

  if [[ ! -r ${buildspec_file} ]] {
    log_error \
      'No buildspec.json found. Please create a build specification for your project.'
    return 2
  }

  local -i debug=0

  local config='RelWithDebInfo'
  local -r -a _valid_configs=(Debug RelWithDebInfo Release MinSizeRel)

  local -i codesign=0
  local -i notarize=0
  local -i package=0

  local -a args
  while (( # )) {
    case ${1} {
      -c|--config)
        if (( # == 1 )) || [[ ${2:0:1} == '-' ]] {
          log_error "Missing value for option %B${1}%b"
          exit 2
        }
        ;;
    }
    case ${1} {
      --) shift; args+=($@); break ;;
      -c|--config)
        if (( !${_valid_configs[(Ie)${2}]} )) {
          log_error "Invalid value %B${2}%b for option %B${1}%b"
          exit 2
        }
        config=${2}
        shift 2
        ;;
      -s|--codesign) typeset -g codesign=1; shift ;;
      -n|--notarize) typeset -g notarize=1; typeset -g codesign=1; shift ;;
      -p|--package) typeset -g package=1; shift ;;
      --debug) debug=1; shift ;;
      *) log_error "Unknown option: %B${1}%b"; exit 2 ;;
    }
  }

  set -- ${(@)args}

  check_macos

  local product_name
  local product_version
  read -r product_name product_version <<< \
    "$(jq -r '. | {name, version} | join(" ")' ${buildspec_file})"

  local output_name="${product_name}-${product_version}-${host_os}-universal"

  if [[ ! -d ${project_root}/release/${config}/${product_name}.plugin ]] {
    log_error 'No release artifact found. Run the build script or the CMake install procedure first.'
    return 2
  }

  if (( package )) {
    log_group "Archiving ${product_name}..."
    pushd ${project_root}/release/${config}
    
    # Copy installation files
    cp ${project_root}/INSTALLATION.md .
    cp ${project_root}/install.sh .
    
    XZ_OPT=-T0 tar -cvJf ${project_root}/release/${output_name}.tar.xz ${product_name}.plugin INSTALLATION.md install.sh
    popd
    
    if [[ ${config} == Release ]] {
      if [[ -d ${project_root}/release/${config}/${product_name}.plugin.dSYM ]] {
        log_group "Archiving ${product_name} Debug Symbols..."
        pushd ${project_root}/release/${config}
        XZ_OPT=-T0 tar -cvJf ${project_root}/release/${output_name}-dSYMs.tar.xz ${product_name}.plugin.dSYM
        popd
      } else {
        log_info "No debug symbols found, skipping dSYM archive creation"
      }
    }
    
    return 0
  } else {
    log_group "Archiving ${product_name}..."
    pushd ${project_root}/release/${config}
    XZ_OPT=-T0 tar -cvJf ${project_root}/release/${output_name}.tar.xz ${product_name}.plugin
    popd
  }

  if [[ ${config} == Release ]] {
    if [[ -d ${project_root}/release/${config}/${product_name}.plugin.dSYM ]] {
      log_group "Archiving ${product_name} Debug Symbols..."
      pushd ${project_root}/release/${config}
      XZ_OPT=-T0 tar -cvJf ${project_root}/release/${output_name}-dSYMs.tar.xz ${product_name}.plugin.dSYM
      popd
    } else {
      log_info "No debug symbols found, skipping dSYM archive creation"
    }
  }

  log_group
}

package ${@}
